class Sequential extends Object {
  __get__(idx) => this.values[idx];
  __set__(idx, val) => {
    this.values[idx] = val;
    return this;
  }
  __len__() => len(this.values);
  __eq__(seq) => {
    if (len(this) != len(seq)) return false;

    for (let i = 0; i < len(this); i = i + 1) {
      if (this[i] != seq[i])
        return false;
    }
    return true;
  }

  __in__(value) => {
    for (x in this)
      if (value == x) return true;

    return false;
  }

  copy() => [x | x in this];
  hash() => hash(join(this.map(str), ""));

  map(fn) => [fn(value) | value in this];

  reduce(fn, acc) => {
    for (x in this)
      acc = fn(acc, x);

    return acc;
  }

  filter(predicate) => [x | x in this, !predicate(x)];

  concat(that) => {
    let seq = [x | x in this];
    for (x in that) seq.push(x);
    return seq;
  }

  some(predicate) => {
    for (x in this)
      if (predicate(x))
        return true;
    return false;
  }

  all(predicate) => {
    for (x in this)
      if (!predicate(x))
        return false;
    return true;
  }

  first(predicate) => {
    for (x in this) {
      if (predicate(x))
        return x;
    }
  }

  find(predicate) => this.first(predicate);

  last() => this.values[len(this) - 1];

  pp() => {
    print this.values;
  }

  fromRaw(raw) => {
    let seq = Sequence();
    for (let i = 0; i < len(raw); i = i + 1)
      seq.push(raw[i]);
    return seq;
  }
}

class Tuple extends Sequential {
  tex(args: ASTArgumentSequence) => "(#{args.tex()})";
}

class Sequence extends Sequential {
  extend(elements) => {
    for (x in elements)
      this.push(x);
  }
}
