
let pp = v => {
  if (v.has("pp")) v.pp();
  else print v;
};

class Constraint {
  init(left, right) => {
    this.left = left;
    this.right = right;
  }

  pp() => {
    pp(this.left);
    print "x";
    pp(this.right);
  }
}

class AppConstraint extends Constraint {}

class Solution extends Map {
  entries() => super.entries().map((e) => Constraint(..e));

  apply(c: Constraint) => {
    for (sub in this) {
      c.left = c.left.substitute(sub);
      c.right = c.right.substitute(sub);
    }

    return c;
  }
  pp() => {
    for (sub in this) { sub.pp(); print ""; }
  }
}

class TypeVarSupply extends NameSupply {
  fresh() => TVar(super.fresh());
}

let globalTypeVarSupply = TypeVarSupply();
let typeVar = globalTypeVarSupply.fresh;

class TUnit extends Type {
  str() => "()";
}
class TVoid extends Type {
  str() => "void";
}
class TBoolean extends Type {
  str() => "bool";
}
class TNumber extends Type {
  str() => "num";
}
class TString extends Type {
  str() => "string";
}

// Transform sequential and unit values into their
// corresponding types, TProduct and TUnit.
let typify = value => {
  if (value == ())
    return unit;
  if (value == undefined)
    return typeVar();
  if (value is Sequential)
    return TProduct(value.map(typify));
  if (vType(value) == OSequence)
    return typify(Sequence.fromRaw(value));
  if (value is TFunction)
    return typify(value.domain) -> typify(value.range);

  return value;
};

class TProduct extends Type {
  init(elements) => {
    this.elements = elements;
  }

  pp() => { print this.elements.values; }

  substitute(c: Constraint) => {
    return TProduct(this.elements.map(x => x.substitute(c)));
  }

  hash() => this.elements.hash();

  __eq__(other) => this.elements == other.elements;
  __len__() => len(this.elements);
}

class TOverload extends Type {
  init(functions) => {
    this.functions = functions;
  }
}

class TClass extends Type {}

let unit = TUnit();
let void = TVoid();
let bool = TBoolean();
let num = TNumber();
let string = TString();
let tClass = TClass();

class SimpleTypeSystem {
  // Map vm types to object language types.
  literalType(value) => {
    let type = vType(value);

    if (type == ()) return unit;
    if (type == CNil) return void;
    if (type == CBool) return bool;
    if (type == CNumber) return num;
    if (type == OString) return string; 
    if (type == OInstance) return value.__class__;
    if (type == OClass) return tClass;
    if (type == OClosure) return TFunction;
  }

  inferType(node: ASTNode) => {
    let state = this.TypeStateClass(this, false);
    state.transform(node);
    let type = state.constrain(node);

    state.solve();

    if (!state.solved())
      throw Error("Can't infer type.");

    type = type.apply(state.solution);

    return type;
  }

  // Assemble and return the [TypeState] produced
  // by [expr].
  typecheck(expr) => {
    let node <- expr;

    let state = this.TypeStateClass(this, false);

    state.transform(node);
    state.constrain(node);
    state.solve();

    return state;
  }

  // Calculate the type of [expr], inferring it if necessary.
  type(expr) => {
    // if it's not a function, return its literal type.
    let exprType = this.literalType(expr);
    if (exprType != TFunction)
      return exprType;
  
    let node: ASTClosure <- expr;
  
    // now check if it has a global type annotation.
    exprType = globalTypeEnv()[node.name];
    if (exprType and globals()[node.name] == expr)
      return exprType;

    // otherwise infer it.
    return this.inferType(node);
  }

  unify(a,b) => this.TypeStateClass(this, false).unify(
    this.ConstraintClass(a,b)
  );
}


class SimpleTypeState {
  init(system: SimpleTypeSystem, debug: bool) => {
    this.system = system;
    // map from variables to types.
    this.typeEnv = Map();
    // constraint stack.
    this.constraints = [];
    // most general set of unifiers.
    this.solution = Solution();
    // print debugging information?
    this.debug = debug;
  }

  // Look up the node in the [typeEnv]. If there's no
  // type entry, then mint a fresh type var.
  lookup(node: ASTVariable) => {
    if (!this.typeEnv[node])
        this.typeEnv[node] = typeVar();

    return this.typeEnv[node];
  }

  // Give [node]'s variables unique identifiers. Map the
  // global variables to their annotations if they exist.
  transform(node: ASTNode) => {
    node.uniquelyIdentifyVariables();

    let annotations = globalTypeEnv();
    node.each(node => {
      if (node is ASTGlobal) {
        if (node.id in annotations)
          this.typeEnv[node] = typify(annotations[node.id]);
      }
    });
  } 

  constrain(node: ASTNode) => {
    if (node is ASTClosure) {
      // give the parameters type vars if they don't
      // have annotations.
      let fnEnv = node.signature.params.reduce(
        (env: Map, param: ASTParameter) => {
          let type = param.type;

          // it's not an ASTNode when we're unifying
          // for pattern matching.
          if (type is ASTNode)
            type = type.resolve();

          env[param] = typify(type);

          return env;
        },
        Map()
      );

      // since the params and their types are keyed by uuid,
      // we can just update the global type env when checking
      // the body of a function.
      this.typeEnv.extend(fnEnv);

      // track the types of the return values as we check the body.
      let returns = node.children.reduce(
        (returns, node) => {
          let type = this.constrain(node);

          if (node is ASTReturn)
            returns.push(type);

          return returns;
        }, []
      );

      // now build the type of the function.
      let tParams;
      let tReturns;

      if (node.signature.arity == 0) {
        tParams = unit;
      } else if (node.signature.arity == 1) {
        tParams = fnEnv.values()[0];
      } else {
        tParams = TProduct(fnEnv.values());
      }

      // there's always an implicit return. for now, if there are
      // explicit returns as well, we'll just ignore the implicit
      // one. once we have ast nodes for conditionals we can be
      // more discriminating.
      let tImplicitReturn = returns.pop();

      if (len(returns) == 0) {
        tReturns = tImplicitReturn;
      } else if (len(returns) == 1) {
        tReturns = returns[0];
      } else {
        tReturns = tReturns;
      }

      return tParams -> tReturns;
    }

    if (node is ASTLiteral)
      return this.system.literalType(node.value);
    
    if (node is ASTNegation)
      return this.constrain(node[0]);

    if (node is ASTGlobal) {
      let value = node.resolve();
      let literalType = this.system.literalType(value);
      let annotationType = this.lookup(node);

      // todo: destructure and reconstruct the type of
      // functions that are defined outside the scope of
      // the check.
      if (literalType == TFunction)
        literalType = typeVar() -> typeVar();

      // annotation and literal type should unify.
      this.constraints.push(Constraint(annotationType, literalType));

      return annotationType;
    }

    if (node is ASTVariable)
      return this.lookup(node);
    
    if (node is ASTArgumentSequence) {
      let tArgs = node.children.map(this.constrain);

      if (len(tArgs) == 0)
        return unit;
      else if (len(tArgs) == 1)
        return tArgs[0];
      else
        return TProduct(tArgs);
    }

    if (node is ASTApp) {
      let func = node[0];
      let args = node[1];

      let tFunc = this.constrain(func);
      let tArgs = this.constrain(args);

      let tApp = typeVar();
      this.constraints.push(Constraint(tFunc, tArgs -> tApp));
      return tApp;
    }

    if (node is ASTLocalTypeAssignment) {
      let var = node[0];
      let val = node[1];

      let tVar = this.lookup(var);
      let tVal = typify(val.resolve());

      this.constraints.push(Constraint(tVar, tVal));

      return void;
    }

    if (node is ASTAssignment) {
      let var = node[0];
      let val = node[1];

      let tVar = this.lookup(var);
      let tVal = this.constrain(val);

      this.constraints.push(Constraint(tVar, tVal));

      // this should eventually respect a possibly user
      // defined __set__ method that returns something.
      return void;
    }

    if (node is ASTExprStatement)
      return this.constrain(node[0]);

    if (node is ASTReturn) 
      return this.constrain(node[0]);

    throw TypeError("Unexpected syntax.");
  }

  unify(constraint: Constraint) => {
    let left = constraint.left,
        right = constraint.right;

    // apply (type variable, type) unifiers to the solution.
    if (left is TVar) {
      this.solution[left] = right;
      this.constraints = this.constraints.map(this.solution.apply);

      return true;
    } else if (right is TVar) {
      this.solution[right] = left;
      this.constraints = this.constraints.map(this.solution.apply);

      return true;
    // function constraints become domain/domain range/range constraints.
    } else if (left is TFunction and right is TFunction) {
      this.constraints.extend([
        Constraint(left.domain, right.domain),
        Constraint(left.range, right.range)
      ]);

      return true;
    // product types of equal length become pairwise sequences of constraints.
    } else if (left is TProduct and right is TProduct) {
      if (len(left) == len(right)) {

        this.constraints.extend(
          zip(left.elements, right.elements).map((x) => Constraint(..x))
        );

        return this.solve();
      } else {
        return false;
      }
    } 

    return left == right;
  }

  // Solve the state's [constraints].
  solve() => {
    let unsolved = [];

    let i = 0;
    while (len(this.constraints) > 0) {
      if (this.debug) {
        print "-------- STEP " + str(i) + " -------------------";
        this.ppConstraints();
        print "---------------------------";
      }

      let constraint = this.constraints.pop();
      let unifiable = this.unify(constraint);

      if (!unifiable)
        unsolved.push(constraint);

      i = i + 1;
    }

    this.constraints = unsolved;

    return this.solved();
  }

  // Compute the most general type for each variable in
  // the original environment.
  principal() => {
    let env = Map();

    for (x in this.typeEnv) {
      let key = x[0];
      let val = x[1];

      if (val is TVar) {
        env[key] = this.solution[val];
      } else {
        env[key] = val;
      }
    }

    return env;
  }

  solved() => len(this.constraints) == 0;
  success() => this.solved();
  failure() => !this.solved();

  ppConstraints() => {
    for (c in this.constraints) {
      print "######## " + str(c.__class__) + " ########";
      c.pp();
      print "############################";
    }
  }
}

SimpleTypeSystem.ConstraintClass = Constraint;
SimpleTypeSystem.TypeStateClass = SimpleTypeState;
