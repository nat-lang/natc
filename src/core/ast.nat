class NameSupply {
  init() => {
    this.next = 0;
  }
  fresh() => {
    this.next = this.next + 1;
    return this.next;
  }
}

class ASTNode extends Tree {

  // Give the tree's variables uuids.
  stampVariables(cb) => {
    let uuids = NameSupply();
    let globals = Map();

    let rename = (node: ASTVar, env: Map) => {
      if (!env[node.data])
        env[node.data] = uuids.fresh();
      node.uuid = env[node.data];
    };

    class VarEnv {
      init() => {
        this.locals = Map();
        this.upvalues = Map();
      }
    }

    this.visit((node, varEnv) => {
      if (node is ASTClosure) {
        let closureEnv = varEnv;
        for (param in node.signature.params) {
          let local = param[0];
          rename(local, closureEnv.locals);
        }
        return closureEnv;
      }
    
      if (node is ASTLocal)
        rename(node, varEnv.locals);
      
      if (node is ASTUpvalue)
        rename(node, varEnv.upvalues);

      if (node is ASTGlobal)
        rename(node, globals);

      cb(node);

      return varEnv;
    }, VarEnv());
  }
}

class ASTStatement extends ASTNode {
  init(*children) => {
    this.children = children;
  }
}

class ASTExpr extends ASTNode {
  init(data) => {
    this.data = data;
    this.children = [];
  }
}

class ASTAssignment extends ASTStatement {}
class ASTLocalValueAssignment extends ASTAssignment {}
class ASTTypeAssignment extends ASTStatement {}
class ASTGlobalTypeAssignment extends ASTTypeAssignment {}
class ASTLocalTypeAssignment extends ASTTypeAssignment {}
class ASTReturn extends ASTStatement {}
class ASTImplicitReturn extends ASTReturn {}
class ASTExprStatement extends ASTStatement {}

class ASTLiteral extends ASTExpr {
  resolve() => this.data;
}
class ASTVar extends ASTExpr {
  init(id) => {
    super.init(id);
    this.uuid = nil;
  }

  hash() => {
    if (!this.uuid) throw Error("Can't hash 'ASTVar' without uuid.");

    return hash(this.uuid);
  }
}

class ASTLocal extends ASTVar {}
class ASTGlobal extends ASTVar {
  resolve() => globals()[this.data];
}
class ASTUpvalue extends ASTVar {}

class ASTArgumentSequence extends ASTExpr {
  init(args) => {
    this.children = args;
  }
  resolve() => this.children.map((node) => node.resolve());
}

class ASTApp extends ASTExprStatement {
  evaluate() => {
    let fn = this[0].resolve();
    let args = this[1].resolve();

    return fn(..args);
  }

  resolve() => this.evaluate();
}

class ASTSignature extends ASTNode {
  init(arity) => {
    this.arity = arity;
    this.params = [];
  }

  isComplete() => len(this.params) == this.arity;
}

class ASTClosure extends ASTExpr {
  init(name, function, arity) => {
    this.name = name;
    this.function = function;
    this.signature = ASTSignature(arity);
    this.children = [];
  }

  resolve() => this.function;

  // expressions.

  opLiteral(value) => ASTLiteral(value);
  opGetLocal(slot) => ASTLocal(slot);
  opGetGlobal(name) => ASTGlobal(name);
  opGetUpvalue(slot) => ASTUpvalue(slot);
  opCall(fn, *args) => ASTApp(fn, ASTArgumentSequence(args));

  // statements.

  opExprStatement(expr) => {
    let node = ASTExprStatement(expr);
    this.children.push(node);
  }

  opSetLocalValue(slot, value) => {
    let local = ASTLocal(slot);
    let node = ASTLocalValueAssignment(local, value);
    this.children.push(node);
  }

  opSetLocalType(slot, value) => {
    let local = ASTLocal(slot);
    let node = ASTLocalTypeAssignment(local, value);

    if (this.signature.isComplete())
      this.children.push(node);
    else
      this.signature.params.push(node);
  }

  opReturn(expr) => {
    let node = ASTReturn(expr);
    this.children.push(node);
  }

  opImplicitReturn(expr) => {
    let node = ASTImplicitReturn(expr);
    this.children.push(node);
  }
}