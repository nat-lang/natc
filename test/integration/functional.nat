
// functions have properties.
// --------------------------------------

// every function has a few default properties.

let f = () => 1;

assert(f.name == "f");
assert(f.arity == 0);
assert(f.variadic == false);
assert(f.patterned == false);

let h = (a) => 1;
assert(h.arity == 1);

let j = (*a) => 1;
assert(j.variadic);

let k = 1 => 1;
assert(k.patterned);

// they haven names even when assigned after declaration.

let g;
g = () => 1;
assert(g.name == "g");

// function properties can be assigned.

f.x = 1;
assert(f.x == 1);

// functions have optional type annotations.
// --------------------------------------

const z;

let a = () => {};

let a = (a) => {};

let a = (a, b) => {};

let a = (a: z) => {};

let a = (a: z, b: z) => {};

let a = (a: z -> z) => {};

let a = (a: z + z - z) => {};

let a = (a: z -> z, b: z) => {};

let a = (a: z, b: z -> z) => {};

let a = (a: z -> z, b: z -> z) => {};

let a = () => x((a: z, b: z) => {});

// function can be anonymous.
// --------------------------------------

// immediate invocation no args.

assert(
  (() => { return 1; })() == 1
);

// immediate invocation with args.

assert(
  ((a) => { return a + 1; })(1) == 2
);

// as return values.

let f = () => {
  return () => { return 1; };
};

assert(f()() == 1);

// as closures.

let f = () => {
  let a = 1;

  return (b) => { return a + b; };
};

assert(f()(1) == 2);

// as arguments.

let compose = (f, g) => {
  return (a) => { return f(g(a)); };
};

let h = compose(
  (a) => { return a + 1; },
  (a) => { return a - 1; }
)(1);

assert(h == 1);

// function declarations with adjacent unparenthesized
// arguments produce curried functions.
// --------------------------------------

// when bound.

let f = x y z => x + y + z;

assert(f(1)(1)(1) == 3);

// and when anonymous.

assert((x => x)(x y z => x + y + z)(1)(1)(1) == 3);

// functions compose.
// --------------------------------------

let a = x => x + 1;
let b = x => x * 2;
let c = x => x / 3;

let g = a . a;

assert(g(1) == 3);

let g = (x => x) . a;

assert(g(1) == 2);

let h = a . b . c;
let j = (a . b) . c;
let k = a . (b . c);

assert(h(1) == j(1));
assert(j(1) == k(1));

// functions can have variable length domains.
// --------------------------------------

// 100% variadic.

let h;

h = (*args) => {
  assert(h.variadic);
  assert(args is Sequence);
  assert(len(args) == 0);
};
h();

h = (*args) => {
  assert(h.variadic);
  assert(args is Sequence);
  assert(len(args) == 1);
  assert(args[0] == 1);
};
h(1);

h = (*args) => {
  assert(h.variadic);
  assert(args is Sequence);
  assert(len(args) == 2);
  assert(args[0] == 1);
  assert(args[1] == 2);
};
h(1,2);

h = (*args) => {
  assert(h.variadic);
  assert(args is Sequence);
  assert(len(args) == 3);
  assert(args[0] == 1);
  assert(args[1] == 2);
  assert(args[2] == 3);
};
h(1,2,3);

// positional and variadic.

h = (x, *args) => {
  assert(x == 1);

  assert(h.variadic);
  assert(args is Sequence);
  assert(len(args) == 0);
};
h(1);

h = (x, y, *args) => {
  assert(x == 1);
  assert(y == 2);

  assert(h.variadic);
  assert(args is Sequence);
  assert(len(args) == 0);
};
h(1,2);

h = (x, *args) => {
  assert(x == 1);

  assert(h.variadic);
  assert(args is Sequence);
  assert(len(args) == 1);
  assert(args[0] == 2);
};
h(1,2);

h = (x, *args) => {
  assert(x == 1);

  assert(h.variadic);
  assert(args is Sequence);
  assert(len(args) == 2);
  assert(args[0] == 2);
  assert(args[1] == 3);
};
h(1,2,3);

h = (x, y, *args) => {
  assert(x == 1);
  assert(y == 2);

  assert(h.variadic);
  assert(args is Sequence);
  assert(len(args) == 1);
  assert(args[0] == 3);
};
h(1,2,3);

h = (x, y, z, *args) => {
  assert(x == 1);
  assert(y == 2);
  assert(z == 3);

  assert(h.variadic);
  assert(args is Sequence);
  assert(len(args) == 0);
};
h(1,2,3);

// patterned and variadic.

let h;

h = (1, *args) => {
  assert(h.patterned);
  assert(h.variadic);
  assert(args is Sequence);
  assert(len(args) == 0);
};
h(1);

// methods can have variable length domains too.

class Variadic {
  call(*args) => args;

  f(*args) => args;
}

let v = Variadic();

assert(v(1,2,3) == [1,2,3]);
assert(v(1,2,3) is Sequence);

assert(v.f(1,2,3) == [1,2,3]);
assert(v.f(1,2,3) is Sequence);

// functions can be partial.
// --------------------------------------

let f = 1 => true;

assert(f(1));
assert(f(2) == undefined);

let f = true => false;

assert(f(true) == false);
assert(f(false) == undefined);

let f = "foo" => "bar";

assert(f("foo") == "bar");
assert(f("bar") == undefined);

// sequences can spread into positional arguments.
// --------------------------------------

// singleton.

let f = (x) => x + 1;
assert(f(..[1]) == 2);

// whole signature.

let f = (x,y) => x + y;
assert(f(..[1,1]) == 2);

let f = (a,b,c,d) => {
  if (a) return b(c,d);
};
let x = [true, +, 1, 3];
assert(f(..x) == 4);

let x = [false, +, 1, 3];
assert(f(..x) == nil);

let x = [true, -, 1, 3];
assert(f(..x) == (1 - 3));

// initial args.

let f = (x,y,z) => {
  z.push(x);
  z.push(y);
  return z;
};

let a = f(..[1,2], []);
assert(a == [1,2]);

// final args.

let f = (x,y,z) => {
  x.push(y);
  x.push(z);
  return x;
};

let a = f([], ..[1,2]);
assert(a == [1,2]);

// & variadic.

let f = (x,*y) => {
  for (z in y) {
    x.push(z);
  }
  return x;
};

let a = f([], ..[1]);
assert(a == [1]);

let a = f([], ..[1,2]);
assert(a == [1,2]);

let a = f([], ..[1,2,3]);
assert(a == [1,2,3]);
